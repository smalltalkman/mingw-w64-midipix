# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=midipix-ntapi
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_base_ver=0.0.0
pkgver=0.0.0.600.f9e9dfd
pkgrel=1
pkgdesc="Native API core library (mingw-w64)"
arch=('any')
url="https://git.midipix.org/ntapi/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-midipix-psxtypes-git"
         "${MINGW_PACKAGE_PREFIX}-midipix-pemagine-git"
         "${MINGW_PACKAGE_PREFIX}-midipix-dalist-git")
makedepends=("git"
             "patch"
             "make"
             "${MINGW_PACKAGE_PREFIX}-gcc")
source=(${_realname}::git://midipix.org/ntapi
        0001-sofort.patch
        0002-project.patch
        0003-src.patch)
sha256sums=('SKIP'
            '9db0aaf9191a1852b23500ce9639bc2096520842a4b11914ccfa50e5f5ae0692'
            '792e7964b51e4e3a702ec399b8d595e3bc5950855ef0d1393ea6eaff6adff303'
            'a0ad3a8efc5c7653a847e5b66a5fd8e38e1a33db3140dcdddc102cf3c0b2ac84')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  rm -rf "${srcdir}/${_realname}.orig"
  cp -rp "${srcdir}/${_realname}" "${srcdir}/${_realname}.orig"
  cd "${srcdir}/${_realname}"
  apply_patch_with_msg \
    0001-sofort.patch \
    0002-project.patch \
    0003-src.patch

  case "${CARCH}" in
    i686)
      export HOST_BITS=32
    ;;
    x86_64)
      export HOST_BITS=64
    ;;
  esac
  export HOST_UNDERSCORE=_
}

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s.%s" "${_base_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make -j1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}/" install

  mkdir -p "${pkgdir}${MINGW_PREFIX}"/bin
  mv "${pkgdir}${MINGW_PREFIX}"/{lib,bin}/libntapi.dll

  cd "${srcdir}/${_realname}"
  for _f in LICENSE COPYING.NTAPI; do
    install -Dm644 ${_f} "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/${_f}"
  done
}
