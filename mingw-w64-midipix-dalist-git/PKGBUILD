# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=midipix-dalist
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_base_ver=0.0.0
pkgver=0.0.0.195.d9ac2c8
pkgrel=1
pkgdesc="A zero-dependency book-keeping library (mingw-w64)"
arch=('any')
url="https://git.midipix.org/dalist/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-midipix-psxtypes-git")
makedepends=("git"
             "patch"
             "make"
             "${MINGW_PACKAGE_PREFIX}-gcc")
source=(${_realname}::git://midipix.org/dalist
        0001-sofort.patch
        0002-project.patch)
sha256sums=('SKIP'
            '9DB0AAF9191A1852B23500CE9639BC2096520842A4B11914CCFA50E5F5AE0692'
            '7628E76A27B1C85EF0E467911D0E7288B8BB37D4FBB29A14BAA24FF4F6F3A7D2')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  rm -rf "${srcdir}/${_realname}.orig"
  cp -rp "${srcdir}/${_realname}" "${srcdir}/${_realname}.orig"
  cd "${srcdir}/${_realname}"
  apply_patch_with_msg \
    0001-sofort.patch \
    0002-project.patch

  export HOST_BITS=32
  export HOST_UNDERSCORE=_
}

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s.%s" "${_base_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make -j1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}/" install

  mkdir -p "${pkgdir}${MINGW_PREFIX}"/bin
  mv "${pkgdir}${MINGW_PREFIX}"/{lib,bin}/libdalist.dll

  cd "${srcdir}/${_realname}"
  for _f in LICENSE COPYING.DALIST; do
    install -Dm644 ${_f} "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/${_f}"
  done
}
