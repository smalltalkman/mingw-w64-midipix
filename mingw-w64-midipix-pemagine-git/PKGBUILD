# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=midipix-pemagine
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_base_ver=0.0.0
pkgver=0.0.0.361.1ca6f15
pkgrel=1
pkgdesc="A (virtual) tour into portable bits and executable bytes (mingw-w64)"
arch=('any')
url="https://git.midipix.org/pemagine/"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-midipix-psxtypes-git")
makedepends=("git"
             "patch"
             "make"
             "${MINGW_PACKAGE_PREFIX}-gcc")
source=(${_realname}::git://midipix.org/pemagine
        0001-sofort.patch
        0002-project.patch)
sha256sums=('SKIP'
            '9db0aaf9191a1852b23500ce9639bc2096520842a4b11914ccfa50e5f5ae0692'
            '45464f4e95e35325865f9bb50bb2445f02c4eb5ffee8caed42171a41d6edecfa')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  rm -rf "${srcdir}/${_realname}.orig"
  cp -rp "${srcdir}/${_realname}" "${srcdir}/${_realname}.orig"
  cd "${srcdir}/${_realname}"
  apply_patch_with_msg \
    0001-sofort.patch \
    0002-project.patch

  case "${CARCH}" in
    i686)
      export HOST_BITS=32
    ;;
    x86_64)
      export HOST_BITS=64
    ;;
  esac
  export HOST_UNDERSCORE=_
}

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s.%s" "${_base_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make -j1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}/" install

  mkdir -p "${pkgdir}${MINGW_PREFIX}"/bin
  mv "${pkgdir}${MINGW_PREFIX}"/{lib,bin}/libpemagine.dll

  cd "${srcdir}/${_realname}"
  for _f in LICENSE COPYING.PEMAGINE; do
    install -Dm644 ${_f} "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/${_f}"
  done
}
